buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
    }
}
apply plugin: 'android'

android {
    compileSdkVersion 'android-21'
    buildToolsVersion '24.0.2'

    defaultConfig {
        minSdkVersion 21
        multiDexEnabled true
    }

    sourceSets {
        main {
            java.srcDirs += 'src/frege'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.frege-lang:frege:3.23.422-ga05a487'
    compile 'com.android.support:multidex:1.0.0'
}

clean {
    delete 'src/frege'
}

// https://groups.google.com/forum/#!msg/frege-programming-language/iIJeAwlf8UM/sqOcu7f6AwAJ

project.afterEvaluate {
    extensions.compileFrege = {
        mkdir 'src/frege'
        description = 'Compile Frege to Java'
        fileTree(dir: 'src/main/frege', include: '**/*.fr').each { file ->
            javaexec {
                configurations.compile.resolve().each {
                    //println it.toString()
                    classpath += files(it.toString())
                }
                main = 'frege.compiler.Main'
                args '-inline', '-d', 'src/frege', '-make', '-sp', 'src/main/frege', '-j', file.path
            }
        }
    }

    compileDebugJavaWithJavac.doFirst { compileFrege() }
    compileReleaseJavaWithJavac.doFirst { compileFrege() }
    // compileDebugJavaWithJack.doFirst { compileFrege() }
    // compileReleaseJavaWithJack.doFirst { compileFrege() }
}
